// Notepad++ Supply Chain Attack Detection
let Lookback = 90d;
// Updater Execution
let SuspiciousUpdaterExecution = 
    DeviceProcessEvents
    | where Timestamp > ago(Lookback)
    | where InitiatingProcessFileName in~ ("update.exe", "gup.exe")
    | where FileName in~ (
        "cmd.exe",
        "powershell.exe", 
        "pwsh.exe",
        "rundll32.exe",
        "mshta.exe", 
        "curl.exe",
        "wget.exe",
        "certutil.exe"
    )
    | where ProcessCommandLine has_any (
        "whoami", "tasklist", "systeminfo", "netstat", "ipconfig",
        "http", "curl", "wget", "certutil", "bitsadmin", 
        "download", "upload",
        "Invoke-Expression", "IEX", "iex", "FromBase64String", "Base64"
    )
    and ProcessCommandLine has_any (">", ">>", ".txt", ".log", "-s", "-F")
    | project
        DeviceId,
        DeviceName,
        UpdateTime = Timestamp,
        UpdaterFile = FileName,
        UpdaterPath = FolderPath,
        UpdaterCommand = ProcessCommandLine,
        UpdaterPID = ProcessId,
        AccountName;
// Post-Update activity
let PostUpdateActivity = 
    DeviceProcessEvents
    | where Timestamp > ago(Lookback)
    | where InitiatingProcessFileName in~ ("update.exe", "gup.exe")
    | where FileName in~ (
        "cmd.exe",
        "powershell.exe",
        "pwsh.exe", 
        "rundll32.exe",
        "mshta.exe",
        "curl.exe"
    )
    | where ProcessCommandLine has_any (
        "whoami",
        "tasklist", 
        "systeminfo",
        "netstat",
        "ipconfig",
        "http",
        "curl"
    )
    and ProcessCommandLine has_any ("-F", "-s", ">", ">>", ".txt")
    | project
        DeviceId,
        ActivityTime = Timestamp,
        ProcessName = FileName,
        CommandLine = ProcessCommandLine,
        ProcessPath = FolderPath,
        ParentProcess = InitiatingProcessFileName,
        ProcessPID = ProcessId;
// Dll Sideloading
let SuspiciousDLLLoads = 
    DeviceImageLoadEvents
    | where Timestamp > ago(Lookback) 
    | where FolderPath has_any ("\\AppData\\", "\\Temp\\")
    // Optional: Known malicious DLLs or suspicious loaders
    | where FileName in~ (
        "log.dll", 
        "alien.dll", 
        "lua5.1.dll", 
        "alien.ini"
    ) 
    or InitiatingProcessFileName in~ (
        "BluetoothService.exe", 
        "script.exe"
    )
    | project
        DeviceId,
        LoadTime = Timestamp,
        LoadedModule = FileName,
        ModulePath = FolderPath,
        LoadingProcess = InitiatingProcessFileName;
// Network Activity
let SuspiciousNetworkConnections = 
    DeviceNetworkEvents
    | where Timestamp > ago(Lookback)
    | where InitiatingProcessFileName in~ (
        "cmd.exe",
        "powershell.exe",
        "pwsh.exe",
        "curl.exe", 
        "rundll32.exe",
        "mshta.exe"
    )
    | project
        DeviceId,
        ConnectionTime = Timestamp,
        NetworkProcess = InitiatingProcessFileName,
        RemoteUrl,
        RemoteIP,
        RemotePort;
// Correlation
SuspiciousUpdaterExecution
| join kind=inner PostUpdateActivity on DeviceId
    | where ActivityTime between (UpdateTime .. (UpdateTime + time(10m)))
| join kind=leftouter SuspiciousDLLLoads on DeviceId
    | where isnull(LoadTime) or LoadTime between (UpdateTime .. (UpdateTime + time(10m)))
| join kind=leftouter SuspiciousNetworkConnections on DeviceId
    | where isnull(ConnectionTime) or ConnectionTime between (UpdateTime .. (UpdateTime + time(10m)))
| project
    Timestamp = UpdateTime,
    DeviceName,
    AccountName,
    UpdaterProcess = UpdaterFile,
    UpdaterPath, 
    UpdaterCommand,
    ChildProcess = ProcessName,
    ChildCommand = CommandLine,
    LoadedModule,
    ModulePath,
    RemoteUrl,
    RemoteIP,
    RemotePort
| order by Timestamp desc
