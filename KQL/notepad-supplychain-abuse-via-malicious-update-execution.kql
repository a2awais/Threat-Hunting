let Lookback = 90d; // adjust Accordignly
// Notepad++ updater execution
let UpdaterExecution =
DeviceProcessEvents
| where Timestamp > ago(Lookback)
| where FileName in~ ("gup.exe", "update.exe")
// | where FolderPath has_any ("\\Notepad++\\", "\\notepad++\\")
| where FolderPath has_any ("\\AppData\\", "\\Temp\\")
    or ProcessCommandLine has_any ("-F", "/S", "/quiet", "silent", "upload")
| project
    DeviceId,
    DeviceName,
    UpdateTime = Timestamp,
    UpdaterFile = FileName,
    UpdaterPath = FolderPath,
    UpdaterCmd = ProcessCommandLine,
    UpdaterPID = ProcessId,
    AccountName;
// Recon + loader activity
let PostUpdateExecution =
DeviceProcessEvents
| where Timestamp > ago(Lookback)
| where InitiatingProcessFileName in~ ("update.exe", "gup.exe")
| where FileName in~ (
    "cmd.exe",
    "powershell.exe",
    "pwsh.exe",
    "rundll32.exe",
    "mshta.exe",
    "curl.exe"
)
| where ProcessCommandLine has_any (
    "whoami",
    "tasklist",
    "systeminfo",
    "netstat",
    "ipconfig",
    "http",
    "curl"
    ) and ProcessCommandLine has_any ("-F", "-s", ">", ">>", ".txt")  // redirection staging
| project
    DeviceId,
    ChildTime = Timestamp,
    ChildProcess = FileName,
    ChildCmd = ProcessCommandLine,
    ChildPath = FolderPath,
    ParentProcess = InitiatingProcessFileName;
// Optional DLL sideload
let SuspiciousImageLoad =
DeviceImageLoadEvents
| where Timestamp > ago(Lookback)
| where FolderPath has_any ("\\AppData\\", "\\Temp\\")
| where FileName in~ ("log.dll") or InitiatingProcessFileName =~ "BluetoothService.exe"  // Optional tighter on known abused binary, Remove for broader hunt
| project
    DeviceId,
    ImageLoadTime = Timestamp,
    LoadedModule = FileName,
    ModulePath = FolderPath,
    LoaderProcess = InitiatingProcessFileName;
// Correlation
UpdaterExecution
| join kind=inner PostUpdateExecution on DeviceId
| where ChildTime between (UpdateTime .. (UpdateTime + time(10m)))
| join kind=leftouter SuspiciousImageLoad on DeviceId
| where
    (ChildProcess in~ ("cmd.exe","powershell.exe","pwsh.exe") and isnotempty(ChildCmd))
    or isnotempty(LoadedModule)
| project
    Timestamp = UpdateTime,
    DeviceName,
    AccountName,
    UpdaterFile,
    UpdaterPath,
    UpdaterCmd,
    ChildProcess,
    ChildCmd,
    LoadedModule,
    ModulePath
| order by Timestamp desc